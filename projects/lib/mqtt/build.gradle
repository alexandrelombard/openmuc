plugins {
    id 'org.jetbrains.kotlin.jvm'
}
def projectName = "OpenMUC Library - MQTT Service"
def projectDescription = "MQTT service for the OpenMUC framework"


def hivemqVersion = '1.2.2'

configurations.create('embed')

dependencies {
    implementation project(':openmuc-core-spi')
    implementation project(':openmuc-lib-filePersistence')
    implementation group: 'com.hivemq', name: 'hivemq-mqtt-client', version: hivemqVersion
    embed group: 'com.hivemq', name: 'hivemq-mqtt-client', version: hivemqVersion
    embed group: 'com.hivemq', name: 'hivemq-mqtt-client-websocket', version: hivemqVersion

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

jar {
    def bundles = '.'
    configurations.embed.each {bundles += ',lib/' + it.name}
    bnd('Bundle-Name': projectName,
            'Bundle-ClassPath': bundles,
            'Export-Package': 'org.openmuc.framework.lib.*',
            'Import-Package': 'org.openmuc.*,org.slf4j.*,javax.*,sun.misc.*'
    )

    into('lib') {
        from configurations.embed
    }
}

// FIXME Temporarily disabling tests
test {
    exclude 'org/openmuc/framework/lib/mqtt/**'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = projectName
                description = projectDescription
            }
        }
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
