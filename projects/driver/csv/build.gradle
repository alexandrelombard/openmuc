plugins {
    id 'org.jetbrains.kotlin.jvm'
}
def projectName = "OpenMUC Driver - CSV"
def projectDescription = "CSV driver for the OpenMUC framework."

def univocityVersion = '2.8.4'

configurations.create('embed')

dependencies {
    implementation project(':openmuc-core-spi')

    implementation group: 'com.univocity', name: 'univocity-parsers', version: univocityVersion
    embed group: 'com.univocity', name: 'univocity-parsers', version: univocityVersion

    testImplementation "org.mockito:mockito-core:2.25.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

jar {
    bnd('Bundle-Name': projectName, 'Bundle-ClassPath': '.,lib/univocity-parsers-' + univocityVersion + '.jar',
            'Import-Package': '!com.univocity.parsers.*,!com.googlecode.openbeans.*,*' + jarDefaultImportPackageVersion)

    into('lib') {
        from configurations.embed
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = projectName
                description = projectDescription
            }
        }
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
